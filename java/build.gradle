subprojects {

    apply plugin: "idea"
    apply plugin: "java"
    apply plugin: "java-test-fixtures"
    apply plugin: "jacoco"

    ext {
        junitVersion = '5.6.2'
        lombokVersion = '1.18.12'
        mockitoVersion = '3.3.3'
        slf4jVersion = '1.7.30'
    }

    repositories {
        jcenter()
        maven { url "https://packages.confluent.io/maven" }
        maven { url "https://jitpack.io" }
    }

    sourceSets {
        integrationTest {
            compileClasspath += sourceSets.main.output
            compileClasspath += sourceSets.testFixtures.output
            runtimeClasspath += sourceSets.main.output
            runtimeClasspath += sourceSets.testFixtures.output
        }
    }

    configurations {
        integrationTestImplementation.extendsFrom implementation
        integrationTestRuntimeOnly.extendsFrom runtimeOnly
    }

    task integrationTest(type: Test) {
        description = 'Runs integration tests.'
        group = 'verification'
        testClassesDirs = sourceSets.integrationTest.output.classesDirs
        classpath = sourceSets.integrationTest.runtimeClasspath
        useJUnitPlatform()
        mustRunAfter jacocoTestReport
    }

    check.dependsOn jacocoTestReport, integrationTest

    test {
        useJUnitPlatform()
    }
}
