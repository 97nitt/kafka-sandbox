plugins {
    id 'java'
    id 'jacoco'
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom implementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly
}

repositories {
    jcenter()
}

dependencies {
    // main dependencies
    annotationProcessor 'org.projectlombok:lombok:1.18.12'
    compileOnly 'org.projectlombok:lombok:1.18.12'
    implementation 'org.apache.kafka:kafka-clients:2.5.0'
    implementation 'org.slf4j:slf4j-api:1.7.30'

    // unit test dependencies
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.mockito:mockito-core:3.3.3'

    // integration test dependencies
    integrationTestImplementation 'junit:junit:4.13'
    integrationTestImplementation 'org.testcontainers:kafka:1.14.1'
    integrationTestRuntimeOnly 'org.slf4j:slf4j-log4j12:1.7.30'
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter jacocoTestReport
}

check.dependsOn jacocoTestReport, integrationTest
